import org.gradle.api.tasks.bundling.AbstractArchiveTask

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
}

android {
    compileSdk 36

    defaultConfig {
        applicationId "eu.thomaskuenneth.codescantoclipboard"
        minSdk 28
        targetSdk 36
        versionCode 22
        versionName "1.3.8"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // F-Droid: Reproducible Builds
            vcsInfo {
                include false
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
        viewBinding true
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    namespace 'eu.thomaskuenneth.codescantoclipboard'

    // F-Droid: Reproducible Builds
    // Disables a proprietary Google blob that changes every build
    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }
}

// F-Droid: Reproducible Builds
// https://f-droid.org/docs/Reproducible_Builds/#publishing-apks-with-the-upstream-developers-signature
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

dependencies {
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.material3
    implementation libs.androidx.material.icons.extended
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material

    debugImplementation libs.androidx.ui.tooling

    implementation libs.androidx.navigation.compose
    implementation libs.androidx.material3.windowsizeclass
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.lifecycle.runtime.compose
    implementation libs.androidx.activity.compose
    implementation libs.androidx.activity.ktx

    implementation libs.zxing.android.embedded
}
